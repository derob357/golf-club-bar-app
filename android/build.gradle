// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "34.0.0"
        minSdkVersion = 23
        compileSdkVersion = 34
        targetSdkVersion = 34
        kotlinVersion = "1.8.0"

        // We use NDK 23 which has both M1 support and is the side-by-side NDK version from AGP.
        ndkVersion = "23.1.7779620"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:8.1.4")
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath("com.google.gms:google-services:4.4.0")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")
    }
}

// Force compatible AndroidX versions for AGP 8.1.4
allprojects {
    configurations.all {
        resolutionStrategy {
            force 'androidx.core:core:1.13.1'
            force 'androidx.core:core-ktx:1.13.1'
        }
    }
}

// Configure subprojects for namespace support
subprojects { subproject ->
    subproject.afterEvaluate {
        if (subproject.hasProperty('android')) {
            // Add namespace for libraries that don't have one
            if (subproject.android.hasProperty('namespace') && !subproject.android.namespace) {
                try {
                    String namespace = subproject.group ?: "com.${subproject.name.replace('-', '.')}"
                    subproject.android.namespace = namespace
                } catch (Exception e) {
                    // Namespace already set, ignore
                }
            }
        }
    }
}
